plugins {
  id 'java-library'
  id 'maven-publish'
  id 'org.jreleaser' version '1.17.0'
}

group = 'io.github.javactrl'
version = '1.1.1'
base {
  archivesName = 'javactrl-delimcc'
}

java {
  withJavadocJar()
  withSourcesJar()
}

repositories {
  mavenCentral()
}

compileJava {
  options.release = 11
}

dependencies {
  testImplementation 'org.junit.jupiter:junit-jupiter:5.12.2'
  testImplementation project(':test-kit')
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  implementation project(':core')
}

jar {
  manifest {
    attributes 'Automatic-Module-Name': 'io.github.javctrl.delimcc'
  }
}

test {
  useJUnitPlatform()
  jvmArgs "-javaagent:${project(":core").jar.archiveFile.get().asFile}"
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = 'javactrl-delimcc'
      from components.java
      pom {
        name = 'javactrl-delimcc'
        description = 'Resumable exceptions for JVM'
        url = 'https://github.com/javactrl'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = 'vitalii.akimov'
            name = 'Vitalii Akimov'
            email = 'vitaliy.akimov@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/javactrl/javactrl.git'
          developerConnection = 'scm:git:ssh://github.com:javactrl/javactrl.git'
          url = 'http://github.com/javactrl/javactrl/tree/master'
        }
      }
    }
  }
  repositories {
    maven {
      url = layout.buildDirectory.dir('staging-deploy')
    }
  }
}

javadoc {
  options.addBooleanOption('html5', true)
}

jreleaser {
  signing {
    active = 'ALWAYS'
    armored = true
  }
  gitRootSearch = true
  deploy {
    maven {
      // nexus2 {
      //   'maven-central' {
      //       active = 'ALWAYS'
      //       url = 'https://s01.oss.sonatype.org/service/local'
      //       snapshotUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
      //       closeRepository = true
      //       releaseRepository = true
      //       stagingRepository('build/staging-deploy')
      //   }
      // }
      mavenCentral {
        sonatype {
          active = 'ALWAYS'
          url = 'https://central.sonatype.com/api/v1/publisher'
          stagingRepository('build/staging-deploy')
        }
      }
    }
  }
}

