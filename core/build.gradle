plugins {
  id 'application'
  id 'maven-publish'
  id 'org.jreleaser' version '1.17.0'
}

group = 'io.github.javactrl'
version = '1.1.1'
base {
  archivesName = 'javactrl-core'
}

java {
  withJavadocJar()
  withSourcesJar()
}

repositories {
  mavenCentral()
}

compileJava {
  options.release = 11
}

dependencies {
  implementation group: 'org.ow2.asm', name: 'asm-util', version: '9.8'
  implementation group: 'org.ow2.asm', name: 'asm-commons', version: '9.8'
  testImplementation 'org.junit.jupiter:junit-jupiter:5.12.2'
  testImplementation project(':test-kit')
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jar {
  manifest {
    attributes 'Main-Class': 'io.github.javactrl.instrument.Main',
      'Premain-Class': 'io.github.javactrl.instrument.Main',
      'Automatic-Module-Name': 'io.github.javactrl.core'
  }
}

test {
  useJUnitPlatform()
  dependsOn = [jar]
  project.afterEvaluate {
    jvmArgs "-javaagent:${jar.archiveFile.get().asFile}"
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      artifactId = 'javactrl-core'
      from components.java
      versionMapping {
        usage('java-api') {
          fromResolutionOf('runtimeClasspath')
        }
        usage('java-runtime') {
          fromResolutionResult()
        }
      }
      pom {
        name = 'javactrl-core'
        description = 'Resumable exceptions for JVM'
        url = 'https://github.com/javactrl'
        inceptionYear = '2022'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = 'vitalii.akimov'
            name = 'Vitalii Akimov'
            email = 'vitaliy.akimov@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/javactrl/javactrl.git'
          developerConnection = 'scm:git:ssh://github.com:javactrl/javactrl.git'
          url = 'http://github.com/javactrl/javactrl/tree/master'
        }
      }
    }
  }
  repositories {
    maven {
      url = layout.buildDirectory.dir('staging-deploy')
    }
  }

}

javadoc {
  options.addBooleanOption('html5', true)
}

jreleaser {
  signing {
    active = 'ALWAYS'
    armored = true
  }
  gitRootSearch = true
  deploy {
    maven {
      mavenCentral {
        sonatype {
          active = 'ALWAYS'
          url = 'https://central.sonatype.com/api/v1/publisher'
          stagingRepository('build/staging-deploy')
        }
      }
    }
  }
}

